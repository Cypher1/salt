-- Test syntax of procs.

-- declarations ---------------------------------------------------------------
term decl1 []: []
 = []

term decl2 [n: #Nat]: #Nat
 = n

term decl3 [n: #Nat]: [] ! #Console
 = box  seq #console'print "hello";
        end

term decl4 []: #Nat ! #Console
 = box  seq #console'print "hello";
        [5]


-- sugar for the above.
proc decl1s []: #Nat
 =      [4]

proc decl2s [n: #Nat]: []
 =      []

proc decl3s [n: #Nat]: []! #Console
 =      seq #console'print "hello";
        []

proc decl3ss [n: #Nat]: []! #Console
 = do   { #console'print "hello"
        ; [] }

proc decl4s []: #Nat ! #Console
 =      seq #console'print "hello";
        5

proc decl4ss []: #Nat ! #Console
 = do   { #console'print "hello"
        ; 5 }


-- sequencing -----------------------------------------------------------------
-- simple sequencing
proc seq0 [x: #Nat, y: #Nat]: #Nat
 =      let a = 2;
        let b = 3;
        #nat'add [a, b]

proc seq0s1 [x: #Nat, y: #Nat]: #Nat
 = do   a = 2
        b = 3
        #nat'add [a, b]

proc seq0s3 []: #Nat
 =      let a = 2 in
        let b = 3 in
        #nat'add [a, b]


-- sugar where the vector of bindings is assumed to be empty.
proc seq1 []: []! #Console
 =      seq #console'print "hello";
        seq #console'print "world";
        seq #console'print "derp";
        end

proc seq1s []: []! #Console
 = do   #console'print ["hello"]
        #console'print "world"
        #console'print "derp"


-- nested procedure.
proc seq'nest1 []: []! #Console
 =      seq  #console'print "good morning";
        seq seq #console'print "hello";
            seq #console'print "world";
            end;
        end

proc seq'nest1s []: []! #Console
 = do   #console'print "good morning"
        do      #console'print "hello"
                #console'print "world"

-- doubly nested procedure.
proc seq'nest2 []: []! #Console
 =      seq #console'print "good morning";
        seq seq #console'print "hello";
            seq seq #console'print "world";
                end;
            end;
        end

-- as above, but with explicit seqs and implicit process end.
proc seq'nest2n []: []! #Console
 =      seq #console'print "good morning";
        seq seq #console'print "hello";
            seq seq #console'print "world"

-- as above, but with explicit do syntax.
proc seq'nest2s []: []! #Console
 = do   { #console'print "good morning"
        ; do    { #console'print "hello"
                ; do    { #console'print "world" }
        }
 }

-- as above, but with implicit do syntax.
proc seq'nest2ss []: []! #Console
 = do   #console'print "good morning"
        do      #console'print "hello"
                do      #console'print "world"


-- cell-introduction ----------------------------------------------------------
proc cell1 [x: #Nat, y: #Nat]: #Nat
 =      cell a: #Nat ← 4;
        cell b: #Nat ← 5;
        #nat'add [a, b]

proc cell1s [x: #Nat, y: #Nat]: #Nat
 = do   cell a: #Nat ← 4
        cell b: #Nat ← 5
        #nat'add [a, b]


-- cell update ----------------------------------------------------------------
proc update1 [x: #Nat, y: #Nat]: #Nat
 =      cell a: #Nat ← x;
        update a ← y;
        a

proc update1s [x: #Nat, y: #Nat]: #Nat
 = do   cell a: #Nat ← x
        update a ← y
        a

proc update1s2 [x: #Nat, y: #Nat]: #Nat
 = do   cell a: #Nat ← x
        a ← y
        a

proc update1s3 [x: #Nat, y: #Nat]: #Nat
 = do   cell a: #Nat ← x
        a ← y
        a ← 5
        a ← 6
        a


-- when -----------------------------------------------------------------------
proc when1 [b: #Bool, x: #Nat, y: #Nat]: #Nat
 =      when b return 5;
        y

proc when1s [b: #Bool, x: #Nat, y: #Nat]: #Nat
 = do   { when b return 5
        ; y }


proc when2 [n: #Nat]: #Text
 =      when (#nat'eq [n, 1]) return "one";
        when (#nat'eq [n, 2]) return "two";
        when (#nat'eq [n, 3]) return "three";
        "dunno"

proc when2s [n: #Nat]: #Text
 = do   when (#nat'eq [n, 1]) return "one"
        when (#nat'eq [n, 2]) return "two"
        when (#nat'eq [n, 3]) return "three"
        "dunno"


proc whens2s [n: #Nat]: #Text
 = do   whens
         #nat'eq [n, 1] → return "one"
         #nat'eq [n, 2] → return "two"
         #nat'eq [n, 3] → return "three"
        "dunno"


proc when3 [n: #Nat, m: #Nat]: #Text
 = do   { when (#nat'eq [n, 1])
                (when (#nat'eq [n, 2])
                        return "derp")
        ; "whatever" }

proc when3s [n: #Nat, m: #Nat]: #Text
 = do   when (#nat'eq [n, 1]) do
                when (#nat'eq [n, 2])
                        return "derp"
        "whatever"

proc when3sd [n: #Nat, m: #Nat]: #Text
 = do   when (#nat'eq [n, 1]) do
                when (#nat'eq [n, 2]) do
                        return "derp"
        "whatever"


proc when4 [n: #Nat, m: #Nat]: #Text
 =      when (#nat'eq [n, 1])
                ( when (#nat'eq [m, 1])
                       (return "one, one")
                ; when (#nat'eq [m, 2])
                       (return "one, two"))
        ; "whatever"

proc when4s [n: #Nat, m: #Nat]: #Text
 = do   { when (#nat'eq [n, 1])
          do    { when (#nat'eq [m, 1])
                        return "one, one"
                ; when (#nat'eq [m, 2])
                        return "one, two"
                }
        ; "whatever" }

proc when4ss [n: #Nat, m: #Nat]: #Text
 = do   { when (#nat'eq [n, 1]) do
                { when (#nat'eq [m, 1]) do
                        { return "one, one" }
                ; when (#nat'eq [m, 2]) do
                        { return "one, two" }
                }
        ; "whatever"
        }

proc when4sss [n: #Nat, m: #Nat]: #Text
 = do   when (#nat'eq [n, 1]) do
                when (#nat'eq [m, 1])
                        return "one, one"
                when (#nat'eq [m, 2])
                        return "one, two"
        "whatever"

proc when4ssss [n: #Nat, m: #Nat]: #Text
 = do   when (#nat'eq [n, 1])
         do     when (#nat'eq [m, 1]) return "one, one"
                when (#nat'eq [m, 2]) return "one, two"
        "whatever"


proc when5 [n: #Nat, m: #Nat]: #Text
 =      whens { #nat'eq [n, 1]
                → whens { #nat'eq [m, 1] → return "one, one"
                        ; #nat'eq [m, 2] → return "one, two" } };
        "whatever"


proc whens5 [n: #Nat, m: #Nat]: #Text
 = do   whens #nat'eq [n, 1]
                → whens #nat'eq [m, 1] → return "one, one"
                        #nat'eq [m, 2] → return "one, two"
        "whatever"


-- match ----------------------------------------------------------------------
-- match does not need to be exhaustive because if we don't have
-- and alt for the given value we continue to the rest of the procedure.
proc match2 [v: <foo: #Nat, bar: #Nat>]: #Nat
 =      match v with
         { foo [x: #Nat] → return x };
        [5]


-- TODO: allow case at top level of final term.
-- These match is exhaustive, so yield at end is dead code.
proc match1 [v: <foo: #Nat, bar: #Nat>]: #Nat
 =      match v with
          { foo [x: #Nat] → return x
          ; bar [x: #Nat] → return x };
        [5]


proc match1s [v: <foo: #Nat, bar: #Nat>]: #Nat
 = do   match v with
         foo [x: #Nat] → return x
         bar [x: #Nat] → return x
        [5]


proc match3 [v: <foo: #Nat, bar: [#Bool, #Nat, #Nat], baz: #Nat>]: #Nat
 =      match v with
         { foo [x: #Nat] → return x
         ; bar [b: #Bool, x: #Nat, y: #Nat]
           → when b return x;
             return y
         ; baz [x: #Nat] → return x };
        0


proc match3s [v: <foo: #Nat, bar: [#Bool, #Nat, #Nat], baz: #Nat>]: #Nat
 = do   { match v with
                { foo [x: #Nat] → return x
                ; bar [b: #Bool, x: #Nat, y: #Nat]
                  → do  { when b do { return x }
                        ; return y
                        }
                ; baz [x: #Nat] → return x }
        ; 0 }


proc match3ss [v: <foo: #Nat, bar: [#Bool, #Nat, #Nat], baz: #Nat>]: #Nat
 = do   match v with
         foo [x: #Nat] → return x
         bar [b: #Bool, x: #Nat, y: #Nat]
          → do  when b return x
                return y
         baz [x: #Nat] → return x

        0


-- loop -----------------------------------------------------------------------
proc loop1 [n: #Nat]: []! #Console
 = launch [] of
   loop when (#nat'gt [n, 10])
                return [];
        #console'print "hello"

proc loop1s [n: #Nat]: []! #Console
 = loop do
        { when (#nat'gt [n, 10])
                return []
        ; #console'print "hello" }


proc loop1ss [n: #Nat]: []! #Console
 = launch [] of
   loop do
        when (#nat'gt [n, 10])
                return []
        #console'print "hello"


-- enter ----------------------------------------------------------------------
proc rec1 [n: #Nat]: #Nat
 = do   cell acc: #Nat ← n
        cell x:   #Nat ← n
        enter fac []
         with fac []: [] = do
                when (#nat'eq [x, 0]) leave
                acc ← #nat'mul [acc, x]
                x   ← #nat'sub [x,   1]
                fac []
        acc

